apply plugin: 'application'

group 'org.comroid'
version '3.00-SNAPSHOT'

apply from: 'gradle/vars.gradle'

mainClassName = 'org.comroid.candybot.CandyBot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.findByName('wrapper')?.configure {
    gradleVersion = '6.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

compileJava.options.encoding = 'UTF-8'

repositories {
    mavenLocal()
    maven {
        url "https://maven.jetbrains.space/comroid/release"
        credentials.username "Anonymous.User"
        credentials.password "anonymous"
    }
    maven {
        url "https://maven.jetbrains.space/comroid/repo"
        credentials.username "Anonymous.User"
        credentials.password "anonymous"
    }
    jcenter()
    maven { url 'https://www.jitpack.io' }
}

evaluationDependsOn ':guardian:uniform'
evaluationDependsOn ':guardian:restless'

dependencies {
    compileOnly 'org.jetbrains:annotations:19.0.0'

    if (isAutoDeploy) {
        implementation 'com.github.comroid-git:comroid.org:master-SNAPSHOT'

        implementation 'com.github.comroid-git.guardian-framework:uniform:master-SNAPSHOT'
        implementation 'com.github.comroid-git.guardian-framework:restless:master-SNAPSHOT'

        implementation 'com.github.comroid-git:cli-parser:master-SNAPSHOT'

        implementation 'com.github.comroid-git:CrystalShard:dev-SNAPSHOT'
    } else {
        implementation project(':status:status-api')

        implementation project(':guardian:uniform').sourceSets.java8compat.output.classesDirs
        implementation project(':guardian:restless').sourceSets.java8compat.output.classesDirs

        implementation project(':clipa')
        implementation project(':common-util:dreadpool')

        implementation project(':CrystalShard')
    }

    // logging dependencies
    implementation 'org.apache.logging.log4j:log4j-api:2.11.0'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.11.0'

    // runtime adapter dependencies
    implementation 'com.squareup.okhttp3:okhttp:4.7.2' // todo Make Optional
    implementation 'com.alibaba:fastjson:1.2.70'
}
